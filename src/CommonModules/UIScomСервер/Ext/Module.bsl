
// ПОЛЯ УВЕДОМЛЕНИЙ по звонкам от UIScom описаны на странице:
// https://help.synergycrm.ru/integratsiya-s-uis 

// Внешние методы
#Область ПрограммныйИнтерфейс

// Функция - JSON в Данные
//
// Параметры:
//  ТелоСтрокой	- Строка - JSON - текст, полученный в запросе
// 
// Возвращаемое значение:
//  Структура - структура полей ИмяПоля = ЗначениеПоля
//
Функция JSON_в_Данные(ТелоСтрокой) Экспорт
	Результат = Неопределено;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	Попытка
		ЧтениеJSON.УстановитьСтроку(ТелоСтрокой);
		Результат = ПрочитатьJSON(ЧтениеJSON);
	Исключение
		Результат = Неопределено;
	КонецПопытки;
	ЧтениеJSON.Закрыть();
	
	Возврат Результат;
КонецФункции

// Функция - Получить структуру полей истории звонков
// делаем нужные преобразования значений в другой тип
//
// Параметры:
//  структураПолей - Структура - поля UIScom из HTTP-уведомлений
// 
// Возвращаемое значение:
//  Структура - Структура полей регистра сведений ИсторияЗвонков
//
Функция ПолучитьСтруктуруПолейИсторииЗвонков(структураПолей) Экспорт 
	стрПолейЗвонков = Новый Структура;
	
	// "Период" из строки "2018-12-18 12:45:59" >> "20181218124559" >> Дату и время
	стрЗаменДаты = "-,:, ";
	МассивЗаменДаты = СтрРазделить(стрЗаменДаты, ",");
	
	СоотвПолей = ПолучитьСоотвПолей();
	Для Каждого эл Из СтруктураПолей Цикл
		ИмяПоляЗвонка  = СоотвПолей.Получить( нрег(эл.Ключ) ); 
		
		Если ИмяПоляЗвонка = Неопределено Тогда // нет соответствия для такого поля - полю в регистре!
			Продолжить;    
			
		ИначеЕсли ИмяПоляЗвонка = "Входящий" Тогда  // в Булево
			значПоляЗвонка = (эл.Значение = "in");
			
		ИначеЕсли ИмяПоляЗвонка = "Период" И ТипЗнч(эл.Значение) = тип("Строка") Тогда // в Дату и Время!
			СтрокаДата = эл.Значение;
			
			Если сред(СтрокаДата, 3, 1) = "." Тогда  // "нормальная" дата "02.02.2022T12:12:12"
				СтрокаДата = СтрЗаменить(СтрокаДата, "T", "");
				значПоляЗвонка = Дата(СтрокаДата);
			Иначе			
				Для i = 0 По МассивЗаменДаты.Количество() - 1 Цикл
				   СтрокаДата = СтрЗаменить(СтрокаДата, МассивЗаменДаты[i], ""); 
				КонецЦикла;
				значПоляЗвонка = Дата(СтрокаДата);
			КонецЕсли;

		ИначеЕсли ИмяПоляЗвонка = "ИдСессии" И ТипЗнч(эл.Значение) = тип("Строка") Тогда // в Число!
			значПоляЗвонка = число(эл.Значение);
			
		Иначе
			значПоляЗвонка = эл.Значение;
		КонецЕсли;	
		
		стрПолейЗвонков.Вставить(ИмяПоляЗвонка, значПоляЗвонка);
	КонецЦикла;	 
	
	Возврат стрПолейЗвонков;
КонецФункции

#КонецОбласти

// Внутренние методы
#Область СлужебныеПроцедурыИФункции

// Функция - Получить соответствие полей
// поля уведомлений POST-запроса от UIScom по звонкам - сопоставляем с полями Рег.Св.ИсторияЗвонков
//
// ПОЛЯ УВЕДОМЛЕНИЙ от UIScom описаны на странице https://help.synergycrm.ru/integratsiya-s-uis 
// подробный API по работе со звонками описан на странице https://comagic.github.io/call-api/
// (здесь НЕ используется!)
// 
// НЕОБРАБАТЫВАЕМЫЕ ПОЛЯ:
// "connection_time",	  	"ВремяСоединения" // поднятие трубки
//
// "notification_name",     "УведомлениеИмя"
// "notification_mnemonic", "УведомлениеКод"
// "notification_time",		"УведомлениеВремя"	
//
// "scenario_name",  		"Сценарий"
// "call_source": 			"Ресурс"
// "employee_phone_number", "Канал"
// 
// Возвращаемое значение:
//  Соответствие - название поля UIScom (в нижнем регистре) или  сопоставляются ВСЕ поля этого регистра сведений!
//
Функция ПолучитьСоотвПолей()
	соотв = Новый Соответствие;
	
	// --------- обязательные Измерения! -----------------   
	соотв.Вставить("start_time", 		  "Период"); // Дата и Время
	соотв.Вставить("virtual_phone_number", "Номер"); // Строка
	соотв.Вставить("direction", 		"Входящий"); // Булево
	соотв.Вставить("call_session_id",   "ИдСессии"); // Число
	
	соотв.Вставить("called_phone_number",  "ЗвонящийНомер");
	соотв.Вставить("calling_phone_number", "ВызываемыйНомер"); 
	
	соотв.Вставить("start_time", "ВремяНачала"); // строка "как есть" 
	соотв.Вставить("total_time_duration", "Длительность");
	соотв.Вставить("wait_time_duration",  "ВремяОжидания");
	
	// --------- поля Centrifugo -----------------------------------	
	соотв.Вставить("send", "Отправлен"); 
	соотв.Вставить("channel", "Канал");
	
	// --------------------- Запись звонка ------------------------	
	соотв.Вставить("file_link", "СсылкаНаЗапись");
	соотв.Вставить("call_record_duration", "ДлительностьЗаписи");
	
	// -------- ВЕСЬ ТЕКСТ ТЕЛА ЗАПРОСА ---------------
	соотв.Вставить("body", "ТелоЗапроса");      
	
	Возврат соотв;
КонецФункции

#КонецОбласти
